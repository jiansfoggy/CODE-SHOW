Command Rectangle L B R T
{
  Line L B R B
  Line R B R T
  Line R T L T
  Line L T L B
}
Command Person PH PV
{
  Rectangle (PH-4) PV PH (PV+2)
  Rectangle (PH+4) PV PH (PV+2)
  Rectangle (PH-3) (PV+2) PH (PV+8)
  Rectangle (PH+3) (PV+2) PH (PV+8)
  Rectangle (PH-5) (PV+6) (PH-3) (PV+14)
  Rectangle (PH+5) (PV+6) (PH+3) (PV+14)
  Rectangle (PH-2) (PV+14) (PH+2) (PV+19)
}
Command PyramidMany H V N
{
  // init vertical position
  Set PY V
  // repeat over levels
  Repeat Y 0 N
  {
    Set Y2 (Y)
    Set DN (Y2*16)
    Set DM (DN/2)
    // center horizontal position
    Set PX (H-DM)
    // repeat over a level
    Repeat X 0 Y
    {
      Person PX PY
      // advance horizontal
      Set PX (PX+16)
    }
    // advance vertical
    Set PY (PY+14)
  }
}
Paper 0
Pen 100
// superhuman strength
PyramidMany 50 0 7
